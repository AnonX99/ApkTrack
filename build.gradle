apply plugin: 'idea'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "fr.kwiatkowski.ApkTrack"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 15
        versionName "2.0.2"
    }

    // Automatic signing for release builds. Credentials and keystore are not present in the VCS.
    def signingPropFile = rootProject.file('signing.properties')
    if (signingPropFile.canRead()) {
        def Properties signingProps = new Properties()
        signingProps.load(new FileInputStream(signingPropFile))
        signingConfigs {
            release {
                storeFile rootProject.file(signingProps['STORE_FILE'])
                keyAlias signingProps['KEY_ALIAS']
                storePassword signingProps['STORE_PASSWORD']
                keyPassword signingProps['KEY_PASSWORD']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:preference-v7:23.1.0'
    compile 'com.android.support:preference-v14:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.github.traex.rippleeffect:library:1.3'
    compile 'ch.acra:acra:4.6.2'

    // TODO: Add SugarORM as a dependency when the BLOB bug is fixed in a release.
    //compile 'com.github.satyan:sugar:1.3.1'
    // SugerORM dependencies:
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.google.guava:guava:18.0'
}

idea {
    module {
        downloadSources = true
    }
}
